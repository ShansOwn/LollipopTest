apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.shansown.android.lollipoptest"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "0.0.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("tests.keystore")
            storePassword "035513"
            keyAlias "debug"
            keyPassword "035513"
        }
        release {
            storeFile file("tests.keystore")
            storePassword "035513"
            keyAlias "release"
            keyPassword "035513"
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->

        variant.outputs.each { output ->
            def apk = output.outputFile;
            def newName;

            if (variant.buildType.name == "release") {
                newName = apk.name.replace(".apk", "-v" + defaultConfig.versionName + "-release.apk");
            } else {
                newName = apk.name.replace(".apk", "-v" + defaultConfig.versionName + "-debug.apk");
            }

            output.outputFile = new File(apk.parentFile, newName);
            if (output.zipAlign) {
                output.outputFile = new File(apk.parentFile, newName.replace("-unaligned", ""));
            }

            logger.info('INFO: Set outputFile to ' + output.outputFile + " for [" + output.name + "]");
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:21.+"
    compile 'com.android.support:recyclerview-v7:21.+'
    compile 'com.android.support:cardview-v7:21.+'
    compile 'com.android.support:palette-v7:21.+'
    compile 'com.android.support:support-v4:21.+'
    compile 'com.squareup.picasso:picasso:2.3.4'
    compile 'de.greenrobot:eventbus:2.2.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.facebook.android:facebook-android-sdk:+'
}

task start(type:Exec) {
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.shansown.android.lollipoptest/.MainActivity'
}